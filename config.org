#+TITLE: Trevoke's emacs config
#+OPTIONS: toc:4

* TODO casual suite (or each package, one at a time)
These are very cool transient menus surfacing the most useful features of various built-in features (calc, dired, re-builder, agenda, and plenty more) and not built-in (avy)

* About
<<babel-init>>
Welcome to my emacs configuration.
I tinker with this file a lot, so I need a quick way to get to it.
I set a [[*Bookmarks][bookmark]] with =C-x r m= and I can get back here with =C-x r b=
* External tools required
- aspell
- revealjs
- hugo
- bibtex
- zotero?
- tree-sitter
- ... lsp servers
* Variables, paths, etc.
#+begin_src elisp
  (setq stag-easy-hugo-basedir "~/src/projects/trevoke.github.io-source"
        stag-easy-hugo-root "~/src/projects/trevoke.github.io"
        stag-org-reveal-root "/home/stag/mnt/internal/src/vendor/reveal.js"
        stag-org-directory "~/my-life/orgnotes/"
        stag-org-gtd-directory "~/my-life/orgnotes/gtd"
        stag-org-roam-directory "~/my-life/orgnotes/roam"
        stag-org-roam-db-location "~/.emacs.d/local-files/org-roam.db"
        stag-bbdb-file "~/my-life/orgnotes/bbdb/big-brother-database"
        stag-bibliography-file "~/my-life/orgnotes/references/library.bib"
        stag-cite-export-processors '((t . (csl "/home/trevoke/my-life/orgnotes/references/modern-language-association.csl")))
        stag-ledger-journal "~/my-life/accounting-personal/2025.ledger"
        )
  ;; CRITICAL: Set org-gtd-directory here so it is defined BEFORE custom-file loads org-gtd-mode
  (setq org-gtd-directory stag-org-gtd-directory)
#+end_src
** Packages (without package management)
#+BEGIN_SRC emacs-lisp
(add-to-list 'load-path "~/.emacs.d/packages")
#+END_SRC
** Themes
#+BEGIN_SRC emacs-lisp
  (add-to-list 'custom-theme-load-path "~/.emacs.d/themes")
  (load-theme 'modus-vivendi t)
  ;; (load-theme 'mejelly t)
#+END_SRC
** Secrets & personal information
API keys, passwords, etc are kept in here. This file is .gitignored.
#+BEGIN_SRC emacs-lisp
(load "~/.emacs.d/.emacs.secrets" t)
#+END_SRC
** asdf (access to programming languages managed with asdf)
#+begin_src emacs-lisp
  (setenv "PATH" (concat (getenv "PATH") ":~/.asdf/shims"))
  (setq exec-path (append exec-path '("~/.asdf/shims")))
#+END_SRC
* Package management
** Quelpa & Use-package
I use [[https://github.com/jwiegley/use-package][use-package]], because:
1. it's got a nice and concise macro interface
2. it allows me to set up emacs on any OS I desire more easily than other options I've encountered so far

[[https://github.com/quelpa/quelpa][Quelpa]] lets me pull from specific source code repos and branches beyond the existing package repositories, which is sometimes convenient.

See the [[./init.el]] file for these things.
* UI
#+begin_src elisp
  (customize-set-variable 'frame-background-mode 'auto)
#+end_src
This is first because nano-theme, while providing a gorgeous UI, has lots of opinions and makes some destructive changes.
#+begin_src elisp
  ;; (use-package nano-theme
  ;;   :ensure nil
  ;;   ;:defer t
  ;;   :quelpa (nano-theme
  ;;            :fetcher github
  ;;            :repo "rougier/nano-theme")
  ;;   :init
  ;;   ;(load-theme 'modus-vivendi-tinted t)
  ;;   :config
  ;;   ;(nano-mode)
  ;;   ;(load-theme 'nano-dark t)
  ;;   )
#+end_src
** Override some nano-theme defaults
#+begin_src elisp
  (setq initial-major-mode 'emacs-lisp-mode)
#+end_src

#+begin_src elisp
  (setq-default cursor-type '(box .  2))
  (setq-default cursor-in-non-selected-windows t)
  (setq blink-cursor-mode t)
#+end_src

#+BEGIN_SRC emacs-lisp
  (setq inhibit-startup-screen t)
  (setq inhibit-startup-echo-area-message t)
  (setq inhibit-startup-message t)
  (setq initial-scratch-message nil)
  (setq inhibit-splash-screen t)

  (show-paren-mode t)
  (setq show-paren-delay 0)

  (global-hl-line-mode t)

  (setq column-number-mode t)
  (setq ring-bell-function 'ignore)
  (setq visible-bell t)
  (setq system-uses-terminfo nil)
#+END_SRC

This shows the file path in the GUI header
#+BEGIN_SRC emacs-lisp
  (setq-default frame-title-format
                '((:eval (if (buffer-file-name)
                             (abbreviate-file-name (buffer-file-name))
                           "%f"))))
#+END_SRC
* General emacs config
** Garbage collection
#+begin_src emacs-lisp
(setq gc-cons-threshold (* 100 1024 1024))
#+end_src
** UTF-8
#+BEGIN_SRC emacs-lisp
;; Activate UTF-8 mode:
(setq locale-coding-system 'utf-8)
(set-terminal-coding-system 'utf-8)
(set-keyboard-coding-system 'utf-8)
(set-selection-coding-system 'utf-8)
(prefer-coding-system 'utf-8)

;; 2013-12-10 IRC #Emacs
(set-clipboard-coding-system 'utf-8)

;; http://www.masteringemacs.org/articles/2012/08/09/working-coding-systems-unicode-emacs/
;; in addition to the lines above:

(set-default-coding-systems 'utf-8)
;; backwards compatibility as default-buffer-file-coding-system
;; is deprecated in 23.2.
(if (boundp 'buffer-file-coding-system)
    (setq-default buffer-file-coding-system 'utf-8)
  (setq default-buffer-file-coding-system 'utf-8))
;; Treat clipboard input as UTF-8 string first; compound text next, etc.
(setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING))
#+END_SRC
** Customizations from customization system
Keeping these separate makes life easier: they get modified when I load a theme and such, and I don't need that kind of hassle when maintaining my actual configuration, you know?
In addition, I do keep some somewhat computer-specific stuff in there, like the font, and it's just easier to have this separated there.
#+BEGIN_SRC emacs-lisp
(setq custom-file "~/.emacs.d/local-files/emacs-custom.el")
(load custom-file t) ;; t means no error if file does not exist
#+END_SRC
** Backups
Centralize emacs backups. This directory is gitignored.
#+BEGIN_SRC emacs-lisp
(setq backup-directory-alist '(("." . "~/.emacs.d/local-files/backups")))
#+END_SRC
And I'm not super-worried about old backups and other such things, so I'll just tell emacs to stop worrying about it.
#+BEGIN_SRC emacs-lisp
  (setq delete-old-versions -1)
  (setq version-control t)
  (setq vc-make-backup-files t)
  (setq auto-save-file-name-transforms
        '((".*" "~/.emacs.d/local-files/auto-save-list/" t)))
  (setq auto-save-list-file-prefix
        "~/.emacs.d/local-files/auto-save-list/.saves-")
#+END_SRC
** Quality of life
*** Yes/No => y/n
#+BEGIN_SRC emacs-lisp
(fset 'yes-or-no-p 'y-or-n-p)
#+END_SRC
*** Recent files
#+begin_src emacs-lisp
  (setq recentf-save-file "~/.emacs.d/local-files/recentf")
  (require 'recentf)
#+end_src
*** History
Stolen from https://www.wisdomandwonder.com/wp-content/uploads/2014/03/C3F.html
#+BEGIN_SRC emacs-lisp
  (setq savehist-file "~/.emacs.d/local-files/savehist")
  (setq history-delete-duplicates t)
  (setq savehist-save-minibuffer-history 1)
  (setq savehist-additional-variables
        '(kill-ring
          search-ring
          regexp-search-ring))
  (savehist-mode 1)
#+END_SRC
*** Bookmarks
#+begin_src emacs-lisp
  (setq bookmark-default-file "~/.emacs.d/local-files/bookmarks")
  (require 'bookmark)
#+end_src
** TODO Window config (Put OSX stuff somewhere else)
I don't really care about any of the extra details - emacs helps me out enough in other ways.
#+BEGIN_SRC emacs-lisp
(when window-system
  (tooltip-mode -1)
  (tool-bar-mode -1)
  (menu-bar-mode -1)
  (scroll-bar-mode -1))
#+END_SRC
Although of course if you're on OSX, at least the menu bar can be enabled since the mighty OSX top bar won't ever disappear.
And I've also added other OSX-specific stuff here... For now.
#+BEGIN_SRC emacs-lisp
  (when (memq window-system '(mac ns))
    (setq mac-command-modifier 'super)
    (setq mac-option-modifier 'meta)
    (setq ns-use-srgb-colorspace t)
    (menu-bar-mode t))
#+END_SRC
** Buffer management
I like unique buffer names. Enter uniquify.
#+BEGIN_SRC emacs-lisp
(require 'uniquify)
(setq
  uniquify-buffer-name-style 'post-forward
  uniquify-separator ":")
#+END_SRC
Also, ibuffer is nicer than plain-old buffer window
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-x C-b") 'ibuffer-other-window)
(setq ibuffer-default-sorting-mode 'major-mode)
#+END_SRC
** Scrolling
#+BEGIN_SRC emacs-lisp
(setq scroll-step 1)
(setq scroll-conservatively 10000)
(setq auto-window-vscroll nil)
(setq mouse-wheel-scroll-amount '(1 ((shift) . 5))) ;; five lines at a time
(setq mouse-wheel-progressive-speed nil) ;; don't accelerate scrolling
(setq mouse-wheel-follow-mouse t) ;; scroll window under mouse
#+END_SRC
* Frame management

** text scaling
This handy little package increases the size of the font in the whole frame.
#+BEGIN_SRC emacs-lisp
(use-package default-text-scale
  :bind (("C-x C-=" . default-text-scale-increase)
         ("C-x C--" . default-text-scale-decrease)))
#+END_SRC
* Window management
** Window layout manager (built-in to emacs)
Prefix key is =C-x C-t=
** Winner-mode (undo/redo window changes)
Remember: =C-c <left>= and =C-c <right>=
#+BEGIN_SRC emacs-lisp
(winner-mode)
#+END_SRC
** Windmove (move across windows with shift+arrow)
#+BEGIN_SRC emacs-lisp
(use-package windmove
:ensure nil
:after org
:config
  (windmove-default-keybindings)
  (add-hook 'org-shiftup-final-hook 'windmove-up)
  (add-hook 'org-shiftleft-final-hook 'windmove-left)
  (add-hook 'org-shiftdown-final-hook 'windmove-down)
  (add-hook 'org-shiftright-final-hook 'windmove-right)
)
#+END_SRC
** Move to new window when splitting
Taken from http://www.reddit.com/r/emacs/comments/25v0eo/you_emacs_tips_and_tricks/chldury
#+BEGIN_SRC emacs-lisp
(defun stag-vsplit-last-buffer (prefix)
  (interactive "p")
  (split-window-vertically)
  (other-window 1 nil)
  (unless prefix
          (switch-to-next-buffer)))

(defun stag-hsplit-last-buffer (prefix)
  (interactive "p")
  (split-window-horizontally)
  (other-window 1 nil)
  (unless prefix
    (switch-to-next-buffer)))

(global-set-key (kbd "C-x 2") 'stag-vsplit-last-buffer)
(global-set-key (kbd "C-x 3") 'stag-hsplit-last-buffer)
#+END_SRC
** Interacting with emacs
*** Fonts and stuff
I found this function online somewhere, before I thought tracking code origin for this config file might matter.
All it does is tell you what face is at point. It toggles hl-line-mode if needed.
#+BEGIN_SRC emacs-lisp
  (defun stag-what-face (pos)
    (interactive "d")
    (let* ((local-hl-p hl-line-mode)
           (global-hl-p global-hl-line-mode)
           (off (if local-hl-p (hl-line-mode -1)))
           (off (if global-hl-p (global-hl-line-mode -1)))
           (face (or (get-char-property pos 'read-face-name)
                     (get-char-property pos 'face))))
      (if face (message "Face: %s" face)
        (message "No face at %d" pos))
      (if local-hl-p (hl-line-mode))
      (if global-hl-p (global-hl-line-mode))))
#+END_SRC
*** Disable C-z to minimize
Suspend emacs？ I'll use C-x C-z.
#+BEGIN_SRC emacs-lisp
(global-unset-key (kbd "C-z"))
(global-unset-key (kbd "C-x C-z"))
#+END_SRC
** Define text interactions
*** Use visual-line-mode
#+BEGIN_SRC emacs-lisp
  (remove-hook 'text-mode-hook #'turn-on-auto-fill)
  (add-hook 'text-mode-hook #'turn-on-visual-line-mode)
  ; when in emacs 30 do this:
  ; (add-hook 'text-mode-hook #'visual-wrap-prefix-mode)
#+END_SRC
*** TODO Redefine kill-region and backward-kill-word (decide whether to keep this?)
I used Bash for a long time. This allows me to keep using Ctrl-w to delete a word backward.
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-w") 'backward-kill-word)
(global-set-key (kbd "C-x C-k") 'kill-region)
#+END_SRC
*** Move down real line by real line
#+BEGIN_SRC emacs-lisp
(setq line-move-visual nil)
#+END_SRC
*** Sentences end with a single space
#+BEGIN_SRC emacs-lisp
(setq sentence-end-double-space nil)
#+END_SRC
*** Inserting new lines before/after current one
#+BEGIN_SRC emacs-lisp
(defun stag-insert-line-below ()
  "Insert and auto-indent line below cursor, like in vim."
  (interactive)
  (move-end-of-line 1)
  (open-line 1)
  (next-line)
  (indent-for-tab-command))

(defun stag-insert-line-above ()
  "Insert and auto-indent line above cursor, like in vim."
  (interactive)
  (previous-line)
  (move-end-of-line 1)
  (stag-insert-line-below))

(global-set-key (kbd "C-o") 'stag-insert-line-below)
(global-set-key (kbd "C-M-o") 'stag-insert-line-above)
#+END_SRC

*** Navigating text
avy is kinda badass.
#+BEGIN_SRC emacs-lisp
(use-package avy
    :bind (("C-c j" . avy-goto-char-2)
           ("C-x j" . avy-po-mark)))
#+END_SRC
* GenAI
#+begin_src emacs-lisp
  (use-package claude-code-ide
    :straight (:type git :host github :repo "manzaltu/claude-code-ide.el")
    :bind ("C-c C-'" . claude-code-ide-menu) ; Set your favorite keybinding
    :config
    (claude-code-ide-emacs-tools-setup)
    :custom
    (claude-code-ide-cli-path "/Users/trace/.local/share/mise/installs/node/20.19.2/bin/claude"))
#+end_src
* Engines

** evil-mode
Because sometimes, vim.
#+BEGIN_SRC emacs-lisp
(use-package evil
  :config
  (evil-mode 1))
#+END_SRC

** Minibuffer
#+begin_src elisp
  (use-package minibuffer
    :ensure nil
    :demand
    :config
    (file-name-shadow-mode 1)
    (minibuffer-depth-indicate-mode 1)
    (minibuffer-electric-default-mode 1)
    :bind (("C-x C-m" . execute-extended-command)))

  (use-package marginalia
    :after (vertico embark consult)
    :init
    (marginalia-mode)
    ;; (advice-add #'marginalia-cycle :after
    ;;             (lambda () (when (bound-and-true-p selectrum-mode) (selectrum-exhibit 'keep-selected))))
    )
#+end_src
*** UI
#+begin_src elisp
  ;(use-package nano-minibuffer
  ;  :ensure nil
  ;  :quelpa (nano-minibuffer
  ;           :fetcher github
  ;           :repo "rougier/nano-minibuffer")
    ;:config
    ;(nano-minibuffer-mode)
  ;  )S
#+end_src
** Command completion
#+BEGIN_SRC emacs-lisp
  (use-package vertico
    :pin elpa
    :init
    (vertico-mode)
    :custom
    (completion-styles '(flex basic partial-completion emacs22)))

  (use-package vertico-prescient
    :after (vertico prescient)
    :config
    (vertico-prescient-mode +1)
    (prescient-persist-mode +1))

  (use-package prescient
    :init
    (setq prescient-save-file "~/.emacs.d/local-files/prescient-save.el")
    :custom
    (prescient-sort-length-enable nil)
    (prescient-filter-method '(literal initialism anchored fuzzy)))

#+END_SRC
** Actions
#+begin_src emacs-lisp
  (use-package embark)

  (use-package embark-consult
    :after (embark consult))
#+end_src
** Input completion
#+begin_src emacs-lisp
  (use-package consult :after vertico)
#+end_src
** Text completion
*** company-mode
#+BEGIN_SRC emacs-lisp
  (use-package company
    :hook (prog-mode . company-mode))
#+END_SRC

** Linting
#+begin_src elisp
  (use-package flycheck)
#+end_src
** LSP (Language Server Protocols)
#+begin_src elisp
        ;; TODO : elixir
        ;; TODO : keymap prefix: C-c i ?
        (use-package eglot
          :ensure nil
          :demand)

        ;(use-package consult-eglot
        ;  :pin "melpa"
        ;  :after (consult eglot))

        ;(use-package consult-eglot-embark
        ;  :pin "melpa"
        ;  :after (consult-eglot embark)
        ;  :config
        ;(consult-eglot-embark-mode))

        (use-package flycheck-eglot
          :pin "melpa"
          :after (flycheck eglot)
          :config (global-flycheck-eglot-mode 1))
#+end_src
** Menus
#+begin_src emacs-lisp
  (assq-delete-all 'transient package--builtins)
  (assq-delete-all 'transient package--builtin-versions)
  (use-package transient
    :init
    (setq transient-history-file "~/.emacs.d/local-files/transient/history.el"
          transient-levels-file "~/.emacs.d/local-files/transient/levels.el"
          transient-values-file "~/.emacs.d/local-files/transient/values.el"))
#+end_src
** Searching
*** Anzu (about search results)
#+BEGIN_SRC emacs-lisp
  (use-package anzu
    :config (global-anzu-mode t)
    :bind (("M-%" . anzu-query-replace)
           ("C-M-%" . anzu-query-replace-regexp)))
#+END_SRC
*** Silver searcher + Wgrep-ag
Sometimes after you've found a bunch of things, you want to edit.. Kind of a find-and-replace sort of deal, maybe?

I forget the basic keybindings all the time: After a search using =ag=, use C-c C-p to start editing the results buffer, and use C-c C-c to save the changes and C-c C-k to cancel.
#+BEGIN_SRC emacs-lisp
(use-package ag
  :config
  (setq ag-reuse-buffers 't))

(use-package wgrep-ag
  :after ag
    :init  (add-hook 'ag-mode-hook 'wgrep-ag-setup)
    :config (autoload 'wgrep-ag-setup "wgrep-ag"))
#+END_SRC
** File (text/code) Structure
#+begin_src elisp
(use-package imenu
  :ensure nil
  :demand
  :config
  (setq imenu-use-markers t)
  (setq imenu-auto-rescan t)
  (setq imenu-auto-rescan-maxout 600000)
  (setq imenu-max-item-length 100)
  (setq imenu-use-popup-menu nil)
  (setq imenu-eager-completion-buffer t)
  (setq imenu-space-replacement " ")
  (setq imenu-level-separator "/"))

(use-package imenu-list
  :after imenu)
#+end_src
** Selecting text
*** Expand region
One of the features that makes IDEA's editors awesome is the way you can expand selection. This plugin replicates the feature.
#+BEGIN_SRC emacs-lisp
  (use-package expand-region
    :bind (("C-c <up>" . er/expand-region)
           ("C-c <down>" . er/contract-region)))
#+END_SRC

* Org-mode
The code to install org-mode itself is in the init.el file (because it is built-in and needs some love).
Here is additional configuration for it.
** libraries for exporting
I've been having some issues exporting, so I'm actively loading libraries here.
#+BEGIN_SRC emacs-lisp
  (use-package ox :ensure nil :after org)
  (use-package org-macro :ensure nil :after org)
  (use-package ob-exp :ensure nil :after org)
  (use-package org-compat :ensure nil :after org)

  (use-package ox-md :ensure nil :after ox)

  ;; (use-package ox-pandoc :after ox)

  (use-package ol-notmuch :after notmuch)
#+END_SRC
** Generic org-mode configuration
#+BEGIN_SRC emacs-lisp
    (setq org-src-fontify-natively t)
    (add-to-list 'auto-mode-alist '(".org.txt$" . org-mode))

    (setq org-startup-indented t)
    (setq org-cycle-separator-lines 0)
    (setq org-goto-interface 'outline-path-completion) ;; useful with C-c C-j
    (setq org-M-RET-may-split-line '((default .t) (headline . nil)))
#+END_SRC
** Org bullets
#+BEGIN_SRC emacs-lisp
  (use-package org-bullets :after org
    :init (add-hook 'org-mode-hook 'org-bullets-mode))
#+END_SRC
** Adding images
#+BEGIN_SRC emacs-lisp
  (use-package org-download :pin "melpa" :after org)
#+END_SRC
** global keybindings
#+BEGIN_SRC emacs-lisp
  (global-set-key "\C-cl" 'org-store-link)
  (global-set-key "\C-cb" 'org-switchb)
  (org-defkey org-mode-map "\C-cr" 'org-refile)
#+END_SRC
* GTD (Getting Things Done)
** Priority and tag inheritance for projects
#+BEGIN_SRC emacs-lisp
  ;;; org-gtd priority and tag inheritance for projects

  (defgroup my/org-gtd-inherit nil
    "Customization for org-gtd priority and tag inheritance."
    :group 'org-gtd
    :prefix "my/org-gtd-inherit-")

  (defcustom my/org-gtd-inherit-priority t
    "Whether to inherit priority from parent project.
When non-nil, project tasks automatically inherit the priority
from their parent project heading."
    :type 'boolean
    :group 'my/org-gtd-inherit)

  (defcustom my/org-gtd-inherit-tags t
    "Whether to inherit tags from parent project.
When non-nil, project tasks automatically inherit the tags
from their parent project heading."
    :type 'boolean
    :group 'my/org-gtd-inherit)

  (defun my/org-gtd-inherit-parent-metadata ()
    "Inherit priority and tags from parent when processing project tasks.
For project tasks, auto-copy priority and tags from parent heading
based on customization variables.  For project headings and other
item types, fall back to normal tag prompting.

Respects `my/org-gtd-inherit-priority' and `my/org-gtd-inherit-tags'
customization variables for selective inheritance."
    (cond
     ;; Case 1: Processing a project task - inherit from parent
     ((org-gtd-organize-type-member-p '(project-task))
      (let ((parent-priority nil)
            (parent-tags nil))
        ;; Save current position, widen to see parent, navigate up, get metadata
        (save-excursion
          (save-restriction
            (widen)  ; CRITICAL: Widen to see parent heading outside narrow region
            (when (org-up-heading-safe)
              (when my/org-gtd-inherit-priority
                (setq parent-priority (org-entry-get (point) "PRIORITY")))
              (when my/org-gtd-inherit-tags
                (setq parent-tags (org-get-tags nil t)))))) ; t = local tags only

        ;; Apply priority if parent has one and inheritance enabled
        (when (and my/org-gtd-inherit-priority parent-priority)
          (org-priority (string-to-char parent-priority)))

        ;; Apply tags if parent has any and inheritance enabled
        (when (and my/org-gtd-inherit-tags parent-tags)
          (org-set-tags parent-tags))))

     ;; Case 2: Processing project heading - prompt normally for tags
     ;; This allows user to set tags on the project itself
     ((org-gtd-organize-type-member-p '(project-heading))
      (call-interactively #'org-set-tags-command))

     ;; Case 3: All other item types - prompt normally
     (t
      (call-interactively #'org-set-tags-command))))
#+END_SRC
** Org-gtd
#+BEGIN_SRC emacs-lisp
  (use-package org-gtd
    :after org ;; '(org org-ql)
    :demand t
    :init
    (setq org-gtd-update-ack "4.0.0")
    :custom
    (org-todo-keywords '((sequence "TODO(t)" "NEXT(n)" "WAIT(w)" "|" "DONE(d)" "CNCL(c)")))
    (org-gtd-keyword-mapping
     '((todo . "TODO")      ;; tasks not ready to be acted upon
       (next . "NEXT")      ;; tasks ready to act on immediately
       (wait . "WAIT")      ;; tasks blocked or delegated
       (done . "DONE")      ;; tasks successfully completed
       (canceled . "CNCL"))) ;; tasks that won't be completed
    (org-priority-highest ?A)
    (org-priority-lowest ?E)
    (org-priority-default ?C)
    (org-gtd-directory stag-org-gtd-directory)
    (org-agenda-files (list org-gtd-directory))
    (org-gtd-agenda-property-position 'next-line)
    (org-edna-use-inheritance t)
    (org-gtd-refile-to-any-target t)
    (org-gtd-organize-hooks '(my/org-gtd-inherit-parent-metadata))
    (org-gtd-clarify-show-horizons 'right)
    (org-gtd-areas-of-focus '("AI AppMod"
                              "BFC"
                              "BFC Sales"
                              "Columbia"
                              "Education"
                              "GCA Admin"
                              "GCA Delivery"
                              "GCA Incubation"
                              "GCA Product"
                              "GCA Sales"
                              "Personal"))
    (org-habit-graph-column 100) ;; Moved from tdw-sanity-view let-binding
    :config
    (org-edna-mode 1)
    (org-gtd-mode 1)
    :bind
    (("C-c d c" . org-gtd-capture)
     ("C-c d e" . org-gtd-engage)
     ("C-c d a" . tdw-sanity-view)
     ("C-c d p" . org-gtd-process-inbox)
     ("C-c d n" . org-gtd-show-all-next)
     ("C-c d s" . org-gtd-show-stuck-projects)
     ("C-c d ?" . org-gtd-command-center)
     ("C-c d g" . org-gtd-show-project-graph)
     :map org-gtd-clarify-map
     ("C-c c" . org-gtd-organize)
     :map org-agenda-mode-map
     ("C-c ?" . org-gtd-agenda-transient)
     ("C-c c" . org-gtd-clarify-agenda-item)))
  (defun tdw-sanity-view ()
    "A focused GTD view: Habits -> Priority A -> Overdue/Today Delegated -> Priority B."
    (interactive)
    (org-gtd-view-show
     '((name . "Sanity View")
       (prefix . (project "—"))
       (blocks . (
                  ;; 1. Habits (Using calendar-day block type to enable habit graph)
                  ((name . "Habits")
                   (block-type . calendar-day)
                   (type . habit))

                  ;; 2. "A" Items
                  ((name . "Must Do (Priority A)")
                   (type . next-action)
                   (priority . A))

                  ;; 3a. Delegated: Overdue (Past)
                  ((name . "Waiting On (Overdue)")
                   (type . delegated)
                   (when . past))

                  ;; 3b. Delegated: Today
                  ((name . "Waiting On (Today)")
                   (type . delegated)
                   (when . today))

                  ;; 4. "B" Items
                  ((name . "Should Do (Priority B)")
                   (type . next-action)
                   (priority . B))
                  )))))
#+END_SRC
** Diary
#+BEGIN_SRC emacs-lisp
(setq diary-file (concat org-directory "diary-file.org"))

(setq view-diary-entries-initially t
      mark-diary-entries-in-calendar t
      diary-number-of-entries 7)
(add-hook 'diary-display-hook 'diary-fancy-display)
#+END_SRC
** Action Tags
These are tags to add to tasks, such as types of action, mindset I need to be in, tool Ι need to have, emotional reward I'll get from doing the action, etc. Whatever helps me pick a task to accomplish.
#+BEGIN_SRC emacs-lisp
  ;; (setq org-tag-alist '(("ai_appmod" . ?a)
  ;;                       ("bfc" . ?b)
  ;;                       ("gca_sales" . ?g)
  ;;                       ("hsbc" . ?h)
  ;;                       ("incubation" . ?i)
  ;;                       ("manhattan" . ?m)
  ;;                       ("menlo_security" . ?e)
  ;;                       ("nvidia" . ?n)
  ;;                       ("panw" . ?p)
  ;;                       ("sales" . ?s)
  ;;                       ("tdw" . ?t)))
#+END_SRC
** Captures
#+BEGIN_SRC emacs-lisp
  (use-package org-capture
    :ensure nil
    :after org
    :config
    (setq org-default-notes-file (concat org-directory "notes.org")))
#+END_SRC
** Refiling
#+BEGIN_SRC emacs-lisp
  (setq org-refile-use-outline-path nil)
  (setq org-outline-path-complete-in-steps nil)
  (setq org-refile-allow-creating-parent-nodes nil)
  (setq org-log-refile 'time)
#+END_SRC
* Zettelkasten
#+BEGIN_SRC emacs-lisp
  (use-package org-roam
    :pin melpa
    :after org
    :ensure t
    :init
    (setq org-roam-db-location stag-org-roam-db-location
          org-roam-v2-ack t)
    :config
    (org-roam-db-autosync-mode)
    ;:hook
    ;(after-init . org-roam-mode)
    :custom
    (org-roam-directory stag-org-roam-directory)
    :bind
    ("C-c n l" . org-roam-buffer-toggle)
    ("C-c n t" . org-roam-dailies-goto-today)
    ("C-c n f" . org-roam-node-find)
    ("C-c n i" . org-roam-node-insert)
    ("C-c n g" . org-roam-graph))

#+END_SRC

* Agenda
#+BEGIN_SRC emacs-lisp
  (use-package org-agenda
    :ensure nil
    :no-require t
    :after org
    :custom
    (org-agenda-diary-file 'diary-file)
    (org-agenda-include-diary t)
    (org-agenda-restore-windows-after-quit t)
    (org-agenda-sticky nil)
    (org-agenda-window-setup 'other-window)
    (org-agenda-skip-deadline-if-done t)
    (org-agenda-skip-scheduled-if-done t)
    (org-agenda-start-on-weekday nil))
#+END_SRC

A quick and pretty agenda view
#+begin_src elisp
;  (use-package nano-agenda
;    :after org-agenda
;    :ensure nil
;    :quelpa (nano-agenda
;             :fetcher github
;             :repo "rougier/nano-agenda"))
#+end_src
* Calendar, dates, times
#+BEGIN_SRC emacs-lisp
  (setq calendar-week-start-day 1) ;; Monday
  (add-hook 'today-visible-calendar-hook 'calendar-mark-today)

#+END_SRC

* File system browsing
** Dired
dired-jump is awesome (=C-x C-j= in any buffer)
#+BEGIN_SRC emacs-lisp
(require 'dired-x)
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; Changed my mind. I prefer seeing just the files:
 (add-hook 'dired-mode-hook 'dired-hide-details-mode)

;; Auto-refresh silently
 (setq global-auto-revert-non-file-buffers t)
 (setq auto-revert-verbose nil)
#+END_SRC

This will make dired behave kinda like a two-pane file manager: with two direds open, you can copy/rename and the default target will be the other pane.
Using split-window-vertically from the first dired might be required to make this work.
#+BEGIN_SRC emacs-lisp
(setq dired-dwim-target t)
#+END_SRC

OSX.. Windows.. *sigh*.
#+BEGIN_SRC emacs-lisp
(setq ls-lisp-use-insert-directory-program nil)
(require 'ls-lisp)
#+END_SRC

It's also nice to have dired with M-< and M-> take you to first and last file
#+BEGIN_SRC emacs-lisp
(require 'dired)
(defun dired-back-to-top ()
  (interactive)
  (beginning-of-buffer)
  (next-line 2))

(define-key dired-mode-map
  (vector 'remap 'beginning-of-buffer) 'dired-back-to-top)

(defun dired-jump-to-bottom ()
  (interactive)
  (end-of-buffer)
  (next-line -1))

(define-key dired-mode-map
  (vector 'remap 'end-of-buffer) 'dired-jump-to-bottom)
#+END_SRC
* Terminal emulators
** vterm
#+begin_src emacs-lisp
(use-package vterm :pin melpa)
#+end_src




* Version control
** Git
*** Walking through a file's history
#+BEGIN_SRC emacs-lisp
(use-package git-timemachine)
#+END_SRC
*** Magit
Magit is a pretty amazing interface to git.
#+BEGIN_SRC emacs-lisp
(use-package magit
  :bind ("C-c g" . magit-status)
  :config (setq magit-last-seen-setup-instructions "1.4.0")
          (setq magit-completing-read-function 'magit-builtin-completing-read)
          (setq magit-popup-use-prefix-argument 'default))
#+END_SRC
**** Magit + gitflow
With this configuration, using C-f in a status buffer will trigger the gitflow selectors.
#+BEGIN_SRC emacs-lisp
  ;; (use-package magit-gitflow
  ;;   :after magit
  ;;   :config (add-hook 'magit-mode-hook 'turn-on-magit-gitflow))
#+END_SRC


